/**
Nightscout glucose display for use with ESPS32-C3 and 1.3" or 1.54" SPI screen.
Based partially on my original version of this for ESP8266: https://github.com/Arakon/Nightscout-TFT
and gluci-clock by Frederic1000: https://github.com/Frederic1000/gluci-clock
Also compatible with other ESP32 devices, but may require pin reassignments below.

*/

// install libraries: WifiManager, arduinoJson, ESP_MultiResetDetector, LovyanGFX


// In the Arduino IDE, select Board Manager and downgrade to ESP32 2.0.14, ESP32 3.x based will NOT fit most smaller ESP32 boards anymore.

// ----------------------------
// Library Defines - Need to be defined before library import
// ----------------------------

#define ESP_MRD_USE_SPIFFS true

#include <Arduino.h>
#include <WiFi.h>
#include <FS.h>
#include <SPIFFS.h>
#include <HTTPClient.h>
#include <time.h>
#include <ArduinoJson.h>
#include <SPI.h>
#include <WiFiManager.h>
//including the arrow images
#include "Flat.h"
#include "DoubleDown.h"
#include "DoubleUp.h"
#include "FortyFiveUp.h"
#include "FortyFiveDown.h"
#include "Up.h"
#include "Down.h"

#define GFXFF 1
#include <LovyanGFX.hpp>
#include "_19_font28pt7b.h"  //Numbers font for the Glucose number
int cleared = 0;             // init for the screen clear command, only to be sent once to prevent visible flickering

//setup for LovyanGFX
class LGFX : public lgfx::LGFX_Device {
  lgfx::Panel_ST7789 _panel_instance;
  lgfx::Bus_SPI _bus_instance;
  lgfx::Light_PWM _light_instance;

public:
  LGFX(void) {
    {  // ---- SPI bus config ----
      auto cfg = _bus_instance.config();
      cfg.spi_host = SPI2_HOST;  // ESP32-C3 uses FSPI (SPI2_HOST)
      cfg.spi_mode = 3;
      cfg.freq_write = 27000000;  // matches your TFT_eSPI SPI_FREQUENCY
      cfg.freq_read = 20000000;   // matches your TFT_eSPI SPI_READ_FREQUENCY
      cfg.spi_3wire = true;
      cfg.use_lock = true;
      cfg.dma_channel = SPI_DMA_CH_AUTO;

      cfg.pin_sclk = 4;   // TFT_SCLK
      cfg.pin_mosi = 6;   // TFT_MOSI
      cfg.pin_miso = -1;  // no MISO on ST7789
      cfg.pin_dc = 8;     // TFT_DC

      _bus_instance.config(cfg);
      _panel_instance.setBus(&_bus_instance);
    }
    {  // ---- Panel config ----
      auto cfg = _panel_instance.config();
      cfg.pin_cs = -1;   // no CS defined in TFT_eSPI setup
      cfg.pin_rst = 10;  // TFT_RST
      cfg.pin_busy = -1;

      cfg.memory_width = 240;
      cfg.memory_height = 240;
      cfg.panel_width = 240;
      cfg.panel_height = 240;
      cfg.offset_x = 0;
      cfg.offset_y = 0;
      cfg.offset_rotation = 0;
      cfg.invert = true;  // ST7789 usually needs this
      cfg.rgb_order = false;
      cfg.dlen_16bit = false;
      cfg.bus_shared = true;

      _panel_instance.config(cfg);
    }
    {  // ---- Backlight (if you have one) ----
      auto cfg = _light_instance.config();
      cfg.pin_bl = -1;  // <- change this if your backlight is connected
      cfg.invert = false;
      cfg.freq = 44100;
      cfg.pwm_channel = 0;
      _light_instance.config(cfg);
      _panel_instance.setLight(&_light_instance);
    }
    setPanel(&_panel_instance);
  }
};

LGFX tft;

#include <ESP_MultiResetDetector.h>
// A library for checking if the reset button has been pressed x amounts of time
// Can be used to enable config mode

int hour_c;
int min_c;
int sec_c;

// These determine when the color of the glucose value changes, edit if you like.
int HighBG = 180;
int LowBG = 90;
int CritBG = 70;

// -------------------------------------
// -------   Other Config   ------
// -------------------------------------

const int PIN_LED = LED_BUILTIN;  // default onboard LED
const int backlight_pin = 0;
int backlight = 64;

#define JSON_CONFIG_FILE "/sample_config.json"

// Number of seconds after reset during which a
// subseqent reset will be considered a triple reset.
#define MRD_TIMES 3
#define MRD_TIMEOUT 5
// RTC Memory Address for the DoubleResetDetector to use
#define MRD_ADDRESS 0


// -----------------------------

// -----------------------------

MultiResetDetector* mrd;

//flag for saving data
bool shouldSaveConfig = false;


// url and API key for Nightscout - You can also set these later in the web interface
// We use the /pebble API since it contains the delta value. Don't remove the /pebble at the end!
char NS_API_URL[150] = "http://yournightscoutwebsite/pebble";

// Create a token with read access in NS > Hamburger menu > Admin tools
// Enter the token here or in the portal
char NS_API_SECRET[50] = "view-123456790";


// Parameters for time NTP server
char ntpServer1[50] = "pool.ntp.org";
char ntpServer2[50] = "de.pool.ntp.org";

// Time zone for local time and daylight saving
// list here:
// https://github.com/nayarsystems/posix_tz_db/blob/master/zones.csv
char local_time_zone[50] = "CET-1CEST,M3.5.0,M10.5.0/3";  // set for Europe/Paris
char utc_time_zone[] = "GMT0";
int gmtOffset_sec = 3600;
long daylightOffset_sec = 3600;  // long

// time zone offset in minutes, initialized to UTC
int tzOffset = 0;

void serialPrintParams() {
  Serial.println("\tNS_API_URL : " + String(NS_API_URL));
  //Serial.println("\tNS_API_SECRET: " + String(NS_API_SECRET));
  Serial.println("\tntpServer1 : " + String(ntpServer1));
  Serial.println("\tntpServer2 : " + String(ntpServer2));
  Serial.println("\tlocal_time_zone : " + String(local_time_zone));
  Serial.println("\tgmtOffset_sec : " + String(gmtOffset_sec));
  Serial.println("\tdaylightOffset_sec : " + String(daylightOffset_sec));
  Serial.println("\tbacklight : " + String(backlight));
  Serial.println("\tHigh BG : " + String(HighBG));
  Serial.println("\tLow BG : " + String(LowBG));
  Serial.println("\tCritical BG : " + String(CritBG));
}

void saveConfigFile() {
  Serial.println(F("Saving config"));
  StaticJsonDocument<512> json;

  json["NS_API_URL"] = NS_API_URL;
  json["NS_API_SECRET"] = NS_API_SECRET;
  json["ntpServer1"] = ntpServer1;
  json["ntpServer2"] = ntpServer2;
  json["local_time_zone"] = local_time_zone;
  json["gmtOffset_sec"] = gmtOffset_sec;
  json["daylightOffset_sec"] = daylightOffset_sec;
  json["backlight"] = backlight;
  json["HighBG"] = HighBG;
  json["LowBG"] = LowBG;
  json["CritBG"] = CritBG;

  File configFile = SPIFFS.open(JSON_CONFIG_FILE, "w");
  if (!configFile) {
    Serial.println("failed to open config file for writing");
  }

  serializeJsonPretty(json, Serial);
  if (serializeJson(json, configFile) == 0) {
    Serial.println(F("Failed to write to file"));
  }
  configFile.close();

  delay(3000);
  ESP.restart();
  delay(5000);
}

bool loadConfigFile() {

  //read configuration from FS json
  Serial.println("mounting FS...");

  // May need to make it begin(true) first time you are using SPIFFS
  // NOTE: This might not be a good way to do this! begin(true) reformats the spiffs
  // it will only get called if it fails to mount, which probably means it needs to be
  // formatted, but maybe dont use this if you have something important saved on spiffs
  // that can't be replaced.
  if (SPIFFS.begin(false) || SPIFFS.begin(true)) {
    Serial.println("mounted file system");
    if (SPIFFS.exists(JSON_CONFIG_FILE)) {
      //file exists, reading and loading
      Serial.println("reading config file");
      File configFile = SPIFFS.open(JSON_CONFIG_FILE, "r");
      if (configFile) {
        Serial.println("opened config file");
        StaticJsonDocument<512> json;
        DeserializationError error = deserializeJson(json, configFile);
        serializeJsonPretty(json, Serial);
        if (!error) {
          strcpy(NS_API_URL, json["NS_API_URL"]);
          strcpy(NS_API_SECRET, json["NS_API_SECRET"]);
          strcpy(ntpServer1, json["ntpServer1"]);
          strcpy(ntpServer2, json["ntpServer2"]);
          strcpy(local_time_zone, json["local_time_zone"]);
          gmtOffset_sec = json["gmtOffset_sec"];
          daylightOffset_sec = json["daylightOffset_sec"];
          backlight = json["backlight"];
          HighBG = json["HighBG"];
          LowBG = json["LowBG"];
          CritBG = json["CritBG"];

          Serial.println("\nThe loaded values are: ");
          serialPrintParams();

          return true;
        } else {
          Serial.println("failed to load json config");
        }
      }
    }
  } else {
    Serial.println("failed to mount FS");
    // SPIFFS.format();
  }
  //end read
  return false;
}




//callback notifying us of the need to save config
void saveConfigCallback() {
  Serial.println("Should save config");
  shouldSaveConfig = true;
}

WiFiManager wm;



// default password for Access Point, made from macID
char* getDefaultPassword() {
  // example:
  // const char * password = getDefaultPassword();

  // source for chipId: Espressif library example ChipId
  uint32_t chipId = 0;
  for (int i = 0; i < 17; i = i + 8) {
    chipId |= ((ESP.getEfuseMac() >> (40 - i)) & 0xff) << i;
  }
  chipId = chipId % 100000000;
  static char pw[9];  // with +1 char for end of chain
  sprintf(pw, "%08d", chipId);
  return pw;
}
const char* apPassword = getDefaultPassword();

//callback notifying us of the need to save parameters
void saveParamsCallback() {
  Serial.println("Should save params");
  shouldSaveConfig = true;
  wm.stopConfigPortal();  // will abort config portal after page is sent
}

// This gets called when the config mode is launced, might
// be useful to update a display with this info.
void configModeCallback(WiFiManager* myWiFiManager) {
  Serial.println("Entered Conf Mode");


  Serial.print("Config SSID: ");
  Serial.println(myWiFiManager->getConfigPortalSSID());
  Serial.print("Config password: ");
  Serial.println(apPassword);
  Serial.print("Config IP Address: ");
  Serial.println(WiFi.softAPIP());

  // Show IP and password for initial setup
  tft.clear(0x000000u);
  tft.setCursor(10, 80);
  tft.setTextSize(2);
  tft.println("Please connect to: ");
  tft.setCursor(10, 100);
  tft.println(WiFi.softAPIP());
  tft.setCursor(10, 130);
  tft.println("Password: ");
  tft.setCursor(10, 150);
  tft.println(apPassword);
  tft.setTextSize(1);
}


/**
 * Set the timezone
 */
void setTimezone(char* timezone) {
  Serial.printf("  Setting Timezone to %s\n", timezone);
  configTzTime(timezone, ntpServer1, ntpServer2);
}

/**
 * Get time from internal clock
 * returns time for the timezone defined by setTimezone(char* timezone)
 */
struct tm getActualTzTime() {
  struct tm timeinfo = { 0 };
  if (!getLocalTime(&timeinfo)) {
    Serial.println("Failed to obtain time!");
    return (timeinfo);  // return {0} if error
  }
  Serial.print("System time: ");
  Serial.println(&timeinfo, "%A, %B %d %Y %H:%M:%S");
  return (timeinfo);
}



/**
 * Returns the offset in seconds between local time and UTC
 */
int getTzOffset(char* timezone) {

  // set timezone to UTC
  setTimezone(utc_time_zone);

  // and get tm struct
  struct tm tm_utc_now = getActualTzTime();

  // convert to time_t
  time_t t_utc_now = mktime(&tm_utc_now);

  // set timezone to local
  setTimezone(local_time_zone);

  // convert time_t to tm struct
  struct tm tm_local_now = *localtime(&t_utc_now);

  // set timezone back to UTC
  setTimezone(utc_time_zone);

  // convert tm to time_t
  time_t t_local_now = mktime(&tm_local_now);

  // calculate difference between the two time_t, in seconds
  int tzOffset = round(difftime(t_local_now, t_utc_now));
  Serial.printf("\nTzOffset : %d\n", tzOffset);

  return (tzOffset);
}

/**
 * Setup : 
 * - connect to wifi, 
 * - evaluate offset between local time and UTC
 */
void setup() {

  Serial.begin(115200);

  Serial.println();
  Serial.println();
  Serial.println("ESP start");

  pinMode(PIN_LED, OUTPUT);
  pinMode(backlight_pin, OUTPUT);

  // Initialize display
  tft.init();
  tft.setRotation(1);
  tft.fillScreen(TFT_BLACK);
  //Print something during boot so you know it's doing something
  analogWrite(backlight_pin, 128);
  tft.setTextColor(TFT_YELLOW, TFT_BLACK);  // Note: the new fonts do not draw the background colour
  tft.setCursor(10, 60);
  tft.setTextSize(2);
  tft.println("Starting up...");  // Due to the way the updates are delayed, it can take up to 60 seconds to get glucose data
  tft.println(" Wait 60 seconds.");
  tft.setTextSize(1);
  Serial.begin(115200);
  Serial.setTimeout(2000);
  Serial.println();
  Serial.println("Starting up...");

  bool forceConfig = false;

  mrd = new MultiResetDetector(MRD_TIMEOUT, MRD_ADDRESS);
  if (mrd->detectMultiReset()) {
    Serial.println(F("Forcing config mode as there was a Triple reset detected"));
    forceConfig = true;
  }

  bool spiffsSetup = loadConfigFile();
  if (!spiffsSetup) {
    Serial.println(F("Forcing config mode as there is no saved config"));
    forceConfig = true;
  }

  WiFi.mode(WIFI_STA);  // explicitly set mode, esp defaults to STA+AP
  Serial.begin(115200);
  delay(10);

  //sets timeout until configuration portal gets turned off
  //useful to make it all retry or go to sleep
  //in seconds
  wm.setTimeout(10 * 60);
  wm.setDarkMode(true);

  //set callbacks
  wm.setSaveConfigCallback(saveConfigCallback);
  wm.setSaveParamsCallback(saveParamsCallback);
  //set callback that gets called when connecting to previous WiFi fails, and enters Access Point mode
  wm.setAPCallback(configModeCallback);

  // Custom parameters here
  wm.setTitle("Nightscout-TFT");

  // Set cutom menu via menu[] or vector
  const char* wmMenu[] = { "param", "wifi", "close", "sep", "info", "restart", "exit" };
  wm.setMenu(wmMenu, 7);  // custom menu array must provide length


  //--- additional Configs params ---

  // url and API key for Nightscout

  WiFiManagerParameter custom_NS_API_URL("NS_API_URL", "NightScout API URL", NS_API_URL, 150);

  WiFiManagerParameter custom_NS_API_SECRET("NS_API_SECRET", "NightScout API secret", NS_API_SECRET, 50);

  // Parameters for time NTP server
  WiFiManagerParameter custom_ntpServer1("ntpServer1", "NTP server 1", ntpServer1, 50);
  WiFiManagerParameter custom_ntpServer2("ntpServer2", "NTP server 2", ntpServer2, 50);
  ;


  // Time zone for local time and daylight saving
  // list here:
  // https://github.com/nayarsystems/posix_tz_db/blob/master/zones.csv
  // char[50] local_time_zone = "CET-1CEST,M3.5.0,M10.5.0/3"; // set for Europe/Paris
  WiFiManagerParameter custom_local_time_zone("local_time_zone", "local_time_zone", local_time_zone, 50);

  //int  gmtOffset_sec = 3600;
  char str_gmtOffset_sec[5];
  sprintf(str_gmtOffset_sec, "%d", gmtOffset_sec);
  WiFiManagerParameter custom_gmtOffset_sec("gmtOffset_sec", "gmtOffset sec", str_gmtOffset_sec, 5);


  // int   daylightOffset_sec = 3600;
  char str_daylightOffset_sec[5];
  sprintf(str_daylightOffset_sec, "%d", daylightOffset_sec);
  WiFiManagerParameter custom_daylightOffset_sec("daylightOffset_sec", "daylightOffset sec", str_daylightOffset_sec, 5);

  char str_backlight[5];
  sprintf(str_backlight, "%u", backlight);
  WiFiManagerParameter custom_backlight("backlight", "Backlight 0-255", str_backlight, 3);

  char str_highbg[5];
  sprintf(str_highbg, "%u", HighBG);
  WiFiManagerParameter custom_highbg("HighBG", "High BG Value", str_highbg, 3);
  char str_lowbg[5];
  sprintf(str_lowbg, "%u", LowBG);
  WiFiManagerParameter custom_lowbg("LowBG", "Low BG Value", str_lowbg, 3);
  char str_critbg[5];
  sprintf(str_critbg, "%u", CritBG);
  WiFiManagerParameter custom_critbg("CritBG", "Critical BG Value", str_critbg, 3);

  // add app parameters to web interface
  wm.addParameter(&custom_NS_API_URL);
  wm.addParameter(&custom_NS_API_SECRET);
  wm.addParameter(&custom_ntpServer1);
  wm.addParameter(&custom_ntpServer2);
  wm.addParameter(&custom_local_time_zone);
  wm.addParameter(&custom_gmtOffset_sec);
  wm.addParameter(&custom_daylightOffset_sec);
  wm.addParameter(&custom_backlight);
  wm.addParameter(&custom_highbg);
  wm.addParameter(&custom_lowbg);
  wm.addParameter(&custom_critbg);


  //--- End additional parameters

  digitalWrite(PIN_LED, LOW);
  if (forceConfig) {
    Serial.println("forceconfig = True");

    if (!wm.startConfigPortal("Nightscout-TFT", apPassword)) {
      Serial.print("shouldSaveConfig: ");
      Serial.println(shouldSaveConfig);
      if (!shouldSaveConfig) {
        Serial.println("failed to connect CP and hit timeout");
        delay(3000);
        //reset and try again, or maybe put it to deep sleep
        ESP.restart();
        delay(5000);
      }
    }
  } else {
    Serial.println("Running wm.autoconnect");

    if (!wm.autoConnect("Nightscout-TFT", apPassword)) {
      Serial.println("failed to connect AC and hit timeout");
      delay(3000);
      // if we still have not connected restart and try all over again
      ESP.restart();
      delay(5000);
    }
  }

  // If we get here, we are connected to the WiFi or should save params
  digitalWrite(PIN_LED, HIGH);

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  // Lets deal with the user config values

  strcpy(NS_API_URL, custom_NS_API_URL.getValue());
  strcpy(NS_API_SECRET, custom_NS_API_SECRET.getValue());
  strcpy(ntpServer1, custom_ntpServer1.getValue());
  strcpy(ntpServer2, custom_ntpServer2.getValue());
  strcpy(local_time_zone, custom_local_time_zone.getValue());
  gmtOffset_sec = atoi(custom_gmtOffset_sec.getValue());
  daylightOffset_sec = atoi(custom_daylightOffset_sec.getValue());
  backlight = atoi(custom_backlight.getValue());
  HighBG = atoi(custom_highbg.getValue());
  LowBG = atoi(custom_lowbg.getValue());
  CritBG = atoi(custom_critbg.getValue());


  Serial.println("\nThe values returned are: ");
  serialPrintParams();

  //save the custom parameters to FS
  if (shouldSaveConfig) {
    saveConfigFile();
  }
  analogWrite(backlight_pin, backlight);

  // offset in seconds between local time and UTC
  tzOffset = getTzOffset(local_time_zone);

  // set timezone to local
  setTimezone(local_time_zone);
}

/**
 * Connect to wifi
 * Retrieve glucose data from NightScout server and parse it to Json

 */
void loop() {
  static int last_minute = -1;   // remembers the last minute we fetched data
  static long last_sgv = -1;     // remembers last glucose value
  static int last_delta = 9999;  // remembers last delta
  time_t now = time(nullptr);
  struct tm tm;
  localtime_r(&now, &tm);

  if (WiFi.status() != WL_CONNECTED) {
    WiFi.reconnect();
  }
  
  // Retrieve glucose data from NightScout Server
  if (tm.tm_min != last_minute) {
    last_minute = tm.tm_min;
    HTTPClient http;
    Serial.println("\n[HTTP] begin...");
    http.begin(NS_API_URL);  // fetch latest value
    http.addHeader("API-SECRET", NS_API_SECRET);
    Serial.print("[HTTP] GET...\n");

    // start connection and send HTTP header
    http.setConnectTimeout(10000);  // 10s
    http.setTimeout(10000);         // 10s
    int httpCode = http.GET();
    // httpCode will be negative on error
    if (httpCode > 0) {
      // HTTP header has been send and Server response header has been handled
      Serial.printf("[HTTP] GET... code: %d\n", httpCode);

      // NightScout data received from server
      if (httpCode == HTTP_CODE_OK) {
        String payload = http.getString();
        Serial.println(payload);

        // parse NightScour data Json response
        // buffer for Json parser
        StaticJsonDocument<256> filter;  // much smaller than 500
        filter["bgs"][0]["sgv"] = true;
        filter["bgs"][0]["trend"] = true;
        filter["bgs"][0]["bgdelta"] = true;
        filter["bgs"][0]["datetime"] = true;
        filter["status"][0]["now"] = true;

        StaticJsonDocument<256> httpResponseBody;
        DeserializationError error = deserializeJson(httpResponseBody, payload, DeserializationOption::Filter(filter));

        // Test if parsing NightScout data succeeded
        if (error) {
          Serial.println();
          Serial.print(F("deserializeJson() failed: "));
          Serial.println(error.f_str());
        } else {

          // Get glucose values, trend, delta and timestamps
          long sgv = long(httpResponseBody["bgs"][0]["sgv"]);
          int trend = httpResponseBody["bgs"][0]["trend"];
          int bg_delta = httpResponseBody["bgs"][0]["bgdelta"];
          long long status0_now = httpResponseBody["status"][0]["now"];
          long long bgs0_datetime = httpResponseBody["bgs"][0]["datetime"];
          int elapsed_mn = (status0_now - bgs0_datetime) / 1000 / 60;
          if (cleared == 0) {  //clear screen only once to get rid of the Startup string), and to reduce refresh flicker
            tft.clear(0x000000u);
            cleared++;
          }
          //add "+" before delta value only if increasing
          String delta = "0";
          String positive = "+";
          if ((bg_delta) >= 0) {
            delta = positive + bg_delta;
          } else {
            delta = (bg_delta);
          }

          int bgcol = TFT_WHITE;
          int agecol = TFT_WHITE;
          int agepos = 38;
          if (sgv > 0) {  //only display if glucose value is valid
            // configure display positions depending on whether glucose is two or three digits
            int digits;
            if ((sgv) < 100) {
              digits = 70;
            } else {
              digits = 30;
            }
            if (sgv != last_sgv) {
              last_sgv = sgv;
              int bgcol = TFT_WHITE;
              if (sgv >= HighBG) bgcol = TFT_ORANGE;
              else if ((sgv <= LowBG) && (sgv > CritBG)) bgcol = TFT_YELLOW;
              else if (sgv <= CritBG) bgcol = TFT_RED;

              tft.fillRect(30, 110, 148, 62, TFT_BLACK);
              tft.setCursor((sgv < 100) ? 70 : 30, 115);
              tft.setFont(&_19_font28pt7b);
              tft.setTextColor(bgcol, TFT_BLACK);
              tft.println(sgv);
            }

            // set color and position of last data depending on amount of digits and time since last value
            if (elapsed_mn >= 15) {
              agecol = TFT_RED;
            } else {
              agecol = TFT_WHITE;
            }
            if (elapsed_mn >= 10) {
              agepos = 30;
            }
            if (elapsed_mn >= 100) {
              agepos = 23;
            }
            // actually display Last Data
            tft.fillRect(0, 0, 240, 28, TFT_BLACK);
            tft.setFont(&fonts::FreeSerifBold9pt7b);
            tft.setTextColor((agecol), TFT_BLACK);
            tft.setCursor(agepos, 4);
            tft.print("Last Data: ");
            tft.print(elapsed_mn);
            if (elapsed_mn == 1) {
              tft.println(" min ago");

            } else {
              tft.println(" mins ago");
            }
          }

          // prepare and display clock, add leading 0 if single digits
          hour_c = (tm.tm_hour);
          min_c = (tm.tm_min);
          tft.fillRect(10, 30, 190, 60, TFT_BLACK);  //clearing time string for less flicker on refresh
          tft.setFont(&fonts::FreeSerifBold24pt7b);
          tft.setTextColor(TFT_BLUE, TFT_BLACK);
          tft.setCursor(65, 40);
          if ((hour_c) < 10) tft.print("0");
          tft.print(hour_c);
          tft.print(":");
          if ((min_c) < 10) tft.print("0");
          tft.print(min_c);

          //Check trend number from json and show the matching arrow
          if ((trend) == 1) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, DoubleUp);
          }
          if ((trend) == 2) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, Up);
          }
          if ((trend) == 3) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, FortyFiveUp);
          }
          if ((trend) == 4) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, Flat);
          }
          if ((trend) == 5) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, FortyFiveDown);
          }
          if ((trend) == 6) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, Down);
          }
          if ((trend) == 7) {
            tft.setSwapBytes(true);
            tft.pushImage(180, 112, 50, 50, DoubleDown);
          }
          if ((trend) == 8) {  //show blank if no valid trend arrow
            tft.fillRect(175, 100, 60, 50, TFT_BLACK);
          }

          // adjust delta position depending on digit count and display
          int deltapos = 54;
          if (bg_delta >= 10) {
            deltapos = 42;
          }
          // redraw delta if it changed
          if (bg_delta != last_delta) {
            last_delta = bg_delta;
            tft.fillRect(35, 180, 200, 60, TFT_BLACK);
            tft.setFont(&fonts::FreeSerifBold18pt7b);
            tft.setTextColor(TFT_BLUE, TFT_BLACK);
            tft.setCursor((bg_delta >= 10) ? 42 : 54, 200);
            tft.print("Delta: ");
            if (bg_delta >= 0) tft.print("+");
            tft.println(bg_delta);
          }
        }
      }
    }


    else {
      Serial.printf("[HTTP] GET... failed, error: %s\n", http.errorToString(httpCode).c_str());
    }

    http.end();
  }
}
